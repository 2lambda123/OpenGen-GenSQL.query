(* Queries *)

query ::= relation-expr | (statement '!') | ('.' command)

statement ::= create-table-stmt
            | create-model-stmt
            | drop-table-stmt
            | drop-model-stmt
            | insert-stmt
            | update-stmt
            | alter-stmt

relation-expr ::= '(' ws? relation-expr ws? ')'
                | relation-expr ';'
                | identifier
                | select-expr
                | generate-expr
                | insert-expr
                | relation-value
                | update-expr
                | alter-expr
                | join-expr
                | generative-join-expr
                | rename-expr
                | with-expr

(* commands *)

command ::= quit-command
          | import-command
          | export-command

quit-command ::= 'quit'

import-command ::= 'import' ws path ws identifier

export-command ::= 'export' ws path ws identifier

path ::= #'[^\s]+'

(* delete *)

create-table-stmt ::= #'(?i)CREATE' ws #'(?i)TABLE' ws identifier ws #'(?i)AS' ws relation-expr

create-model-stmt ::= #'(?i)CREATE' ws #'(?i)MODEL' ws identifier ws #'(?i)AS' ws model-expr

drop-table-stmt ::= #'(?i)DROP' ws #'(?i)TABLE' (ws #'(?i)IF' ws #'(?i)EXISTS')? ws identifier

drop-model-stmt ::= #'(?i)DROP' ws #'(?i)MODEL' (ws #'(?i)IF' ws #'(?i)EXISTS')? ws identifier

insert-stmt ::= #'(?i)INSERT' ws #'(?i)INTO' ws identifier ws relation-expr

update-stmt ::= #'(?i)UPDATE' ws identifier
                ws #'(?i)SET' ws update-settings
                (ws #'(?i)WHERE' ws scalar-expr)?

alter-stmt ::= #'(?i)ALTER' ws identifier ws #'(?i)ADD' ws identifier

(* with-expr *)

with-expr ::= #'(?i)WITH' ws binding (ws? ',' ws? binding)* ws? ':' ws? relation-expr
binding ::= expr ws alias-clause
<expr> ::= relation-expr | model-expr | scalar-expr

(* select *)

select-expr ::= select-clause
                ws from-clause
                (ws where-clause)?
                (ws group-by-clause)?
                (ws order-by-clause)?
                (ws distinct-clause)?
                (ws limit-clause)?

distinct-clause ::= #'(?i)DISTINCT'

select-clause ::= #'(?i)SELECT' (ws distinct-clause)? ws select-list

select-list ::= select-star-clause
              / selection (ws? ',' ws? selection)*
              / aggregation (ws? ',' ws? aggregation)*

select-star-clause ::= star (ws? select-except-clause)?
star ::= '*'
select-except-clause ::= #'(?i)EXCEPT' ws? '(' ws? identifier-list ws? ')'

selection ::= (scalar-expr | aggregation) (ws alias-clause)?

alias-clause ::= #'(?i)AS' ws identifier

from-clause ::= #'(?i)FROM' ws relation-expr (ws? alias-clause)?

where-clause ::= #'(?i)WHERE' ws scalar-expr

group-by-clause ::= #'(?i)GROUP' ws #'(?i)BY' ws identifier (ws? "," ws? identifier)*

order-by-clause ::= #'(?i)ORDER' ws #'(?i)BY' ws identifier (ws (asc | desc))?

asc  ::= #'(?i)ASC'
desc ::= #'(?i)DESC'

limit-clause ::= #'(?i)LIMIT' ws int

(* aggregation *)

aggregation ::= aggregation-fn ws? '(' (distinct-clause ws)? ws? (star / identifier) ws? ')' (ws alias-clause)?
aggregation-fn ::= count | avg | median | std | max | min | sum

count  ::= #'(?i)COUNT'
avg    ::= #'(?i)AVG'
std    ::= #'(?i)STD'
median ::= #'(?i)MEDIAN'
max    ::= #'(?i)MAX'
min    ::= #'(?i)MIN'
sum    ::= #'(?i)SUM'

(* scalar-expr *)

scalar-expr ::= scalar-expr-0

<scalar-expr-0> ::= scalar-expr-1 | expr-disjunction
<scalar-expr-1> ::= scalar-expr-2 | expr-conjunction
<scalar-expr-2> ::= scalar-expr-3 | expr-not
<scalar-expr-3> ::= scalar-expr-4 | expr-binop
<scalar-expr-4> ::= scalar-expr-5 | expr-addition | expr-subtraction
<scalar-expr-5> ::= scalar-expr-6 | expr-multiplication | expr-division
<scalar-expr-6> ::= scalar-expr-7 | expr-function-call

expr-disjunction ::= scalar-expr-0 ws #'(?i)OR'  ws scalar-expr-1
expr-conjunction ::= scalar-expr-1 ws #'(?i)AND' ws scalar-expr-2

expr-not ::= #'(?i)NOT' ws scalar-expr-3

expr-binop ::= scalar-expr-3 ws? binop ws? scalar-expr-4

expr-addition    ::= scalar-expr-4 ws? '+' ws? scalar-expr-5
expr-subtraction ::= scalar-expr-4 ws? '-' ws? scalar-expr-5

expr-multiplication ::= scalar-expr-5 ws? '*' ws? scalar-expr-6
expr-division       ::= scalar-expr-5 ws? '/' ws? scalar-expr-6

(* currently only log - will likely add more later *)
<expr-function-call> ::=  expr-function-call-log
expr-function-call-log ::= 'log(' ws? scalar-expr-6 ws? ')'

scalar-expr-group ::= '(' ws? scalar-expr ws? ')'

(* insert-expr *)

insert-expr ::= #'(?i)INSERT' ws #'(?i)INTO' ws relation-expr ws relation-expr

relation-value ::= '(' ws? identifier-list ws? ')' ws #'(?i)VALUES' ws value-lists

value-list ::= '(' ws? value (ws? ',' ws? value)* ws? ')'
value-lists ::= value-lists-full | value-lists-sparse
value-lists-full ::= value-list (ws? ',' ws? value-list)*
value-lists-sparse ::= '...' ws? nat ws? ':' ws? value-list (ws? ',' ws? nat ws? ':' ws? value-list)* ws? '...'

(* update-expr *)

update-expr ::= #'(?i)UPDATE' ws relation-expr
                ws #'(?i)SET' ws update-settings
                (ws #'(?i)WHERE' ws scalar-expr)?
update-settings ::= update-setting (ws? ',' ws? update-setting)*
update-setting ::= identifier ws? '=' ws? scalar-expr

(* alter-expr *)

alter-expr ::= #'(?i)ALTER' ws relation-expr ws #'(?i)ADD' ws identifier

(* rename-expr *)

rename-expr ::= relation-expr ws alias-clause

(* join-expr *)

join-expr ::= join-expr-0

<join-expr-0> ::= join-expr-1 | cross-join-expr | inner-join-expr
<join-expr-1> ::= join-expr-group

cross-join-expr ::= relation-expr ws #'(?i)CROSS' ws #'(?i)JOIN' ws relation-expr
inner-join-expr ::= relation-expr (ws #'(?i)INNER')? ws #'(?i)JOIN' ws relation-expr ws #'(?i)ON' ws scalar-expr
join-expr-group ::= '(' join-expr ')'

(* generate-expr *)

generate-expr ::= #'(?i)GENERATE' ws generate-list ws #'(?i)UNDER' ws model-expr
<generate-list> ::= generate-star-clause
                  / model-var-list
generate-star-clause ::= star (ws? generate-except-clause)?
generate-except-clause ::= #'(?i)EXCEPT' ws? '(' ws? model-var-list ws? ')'

(* generative-join-expr *)

generative-join-expr ::= relation-expr ws #'(?i)GENERATIVE' ws #'(?i)JOIN' ws model-expr

(* models, conditioning, constraining, and events *)

generative-table-expr ::= #'(?i)GENERATIVE' ws #'(?i)TABLE' ws relation-expr

distribution-event ::= distribution-event-0

<distribution-event-0> ::= distribution-event-1 | distribution-event-or
<distribution-event-1> ::= distribution-event-2 | distribution-event-and
<distribution-event-2> ::= distribution-event-3 | distribution-event-binop
<distribution-event-3> ::= distribution-event-group

distribution-event-or  ::= distribution-event-0 ws #'(?i)OR'  ws distribution-event-1
distribution-event-and ::= distribution-event-1 ws #'(?i)AND' ws distribution-event-2

distribution-event-group ::= '(' ws? distribution-event ws? ')'

constrained-by-expr ::= model-expr
                        ws #'(?i)CONSTRAINED' ws #'(?i)BY'
                        ws distribution-event

density-event ::= density-event-0

<density-event-0> ::= density-event-1 | density-event-and
<density-event-1> ::= density-event-2 | density-event-eq
<density-event-2> ::= density-event-group

density-event-and ::= density-event-1 (ws #'(?i)AND' ws density-event-1)+

density-event-group ::= '(' ws? density-event ws? ')'

conditioned-by-expr ::= model-expr ws #'(?i)CONDITIONED' ws #'(?i)BY' ws ('*' | density-event)

incorporate-expr ::= #'(?i)INCORPORATE' ws relation-expr ws #'(?i)INTO' ws model-expr

variable ::= #'(?i)VAR' ws identifier

variable-list ::= variable (ws? ',' ws? variable)*

(* binop *)

binop ::= '>' | '>=' | '=' | '<=' | '<' | is | is-not
is ::= #'(?i)IS'
is-not ::= #'(?i)IS' ws #'(?i)NOT'

(* literals *)

value            ::= float | int | bool | string | null
bool             ::= #'true|false'
float            ::= #'-?\d+\.\d+(E-?\d+)?'
int              ::= #'-?\d+'
nat              ::= #'\d+'
identifier-list  ::= identifier (ws? ',' ws? identifier)*
identifier       ::= simple-symbol | delimited-symbol
simple-symbol    ::= #'(?u)(?!G__)\p{L}[\p{L}\p{N}_\-\?\.]*'
delimited-symbol ::= <'"'> #'[^\"]+' <'"'>
string           ::= <"'"> #"[^']*" <"'">
null             ::= #'(?i)NULL'

(* whitespace *)

ws ::= #'\s+'
(* TODO: add SQL "--" and "/* */" comments *)
