(* Queries *)

query ::= relation-expr | (statement '!') | ('.' command)

statement ::= create-table-stmt
            | create-model-stmt
            | drop-table-stmt
            | drop-model-stmt
            | insert-stmt
            | update-stmt
            | alter-stmt

relation-expr ::= '(' ws? relation-expr ws? ')'
                | relation-expr ';'
                | simple-symbol
                | select-expr
                | generate-expr
                | insert-expr
                | relation-value
                | update-expr
                | alter-expr
                | join-expr
                | rename-expr
                | with-expr

(* commands *)

command ::= quit-command
          | import-command
          | export-command

quit-command ::= 'quit'

import-command ::= 'import' ws path ws simple-symbol

export-command ::= 'export' ws path ws simple-symbol

path ::= #'[^\s]+'

(* delete *)

create-table-stmt ::= #'(?i)CREATE' ws #'(?i)TABLE' ws simple-symbol ws #'(?i)AS' ws relation-expr

create-model-stmt ::= #'(?i)CREATE' ws #'(?i)MODEL' ws simple-symbol ws #'(?i)AS' ws model-expr

drop-table-stmt ::= #'(?i)DROP' ws #'(?i)TABLE' (ws #'(?i)IF' ws #'(?i)EXISTS')? ws simple-symbol

drop-model-stmt ::= #'(?i)DROP' ws #'(?i)MODEL' (ws #'(?i)IF' ws #'(?i)EXISTS')? ws simple-symbol

insert-stmt ::= #'(?i)INSERT' ws #'(?i)INTO' ws simple-symbol ws relation-expr

update-stmt ::= #'(?i)UPDATE' ws simple-symbol
                ws #'(?i)SET' ws update-settings
                (ws #'(?i)WHERE' ws scalar-expr)?

alter-stmt ::= #'(?i)ALTER' ws simple-symbol ws #'(?i)ADD' ws simple-symbol

(* with-expr *)

with-expr ::= #'(?i)WITH' ws binding (ws? ',' ws? binding)* ws? ':' ws? relation-expr
binding ::= expr ws alias-clause
<expr> ::= relation-expr | model-expr | scalar-expr

(* select *)

select-expr ::= select-clause
                ws from-clause
                (ws where-clause)?
                (ws group-by-clause)?
                (ws order-by-clause)?
                (ws distinct-clause)?
                (ws limit-clause)?

distinct-clause ::= #'(?i)DISTINCT'

select-clause ::= #'(?i)SELECT' (ws distinct-clause)? ws select-list

select-list ::= star
              / selection (ws? ',' ws? selection)*
              / aggregation (ws? ',' ws? aggregation)*

star ::= '*'

selection ::= (scalar-expr | aggregation) (ws alias-clause)?

alias-clause ::= #'(?i)AS' ws simple-symbol

from-clause ::= #'(?i)FROM' ws relation-expr (ws? alias-clause)?

where-clause ::= #'(?i)WHERE' ws scalar-expr

group-by-clause ::= #'(?i)GROUP' ws #'(?i)BY' ws simple-symbol (ws? "," ws? simple-symbol)*

order-by-clause ::= #'(?i)ORDER' ws #'(?i)BY' ws simple-symbol (ws (asc | desc))?

asc  ::= #'(?i)ASC'
desc ::= #'(?i)DESC'

limit-clause ::= #'(?i)LIMIT' ws int

(* aggregation *)

aggregation ::= aggregation-fn ws? '(' (distinct-clause ws)? ws? (star / simple-symbol) ws? ')' (ws alias-clause)?
aggregation-fn ::= count | avg | median | std | max | min | sum

count  ::= #'(?i)COUNT'
avg    ::= #'(?i)AVG'
std    ::= #'(?i)STD'
median ::= #'(?i)MEDIAN'
max    ::= #'(?i)MAX'
min    ::= #'(?i)MIN'
sum    ::= #'(?i)SUM'

(* scalar-expr *)

scalar-expr ::= scalar-expr-0

<scalar-expr-0> ::= scalar-expr-1 | expr-disjunction
<scalar-expr-1> ::= scalar-expr-2 | expr-conjunction
<scalar-expr-2> ::= scalar-expr-3 | expr-not
<scalar-expr-3> ::= scalar-expr-4 | expr-binop
<scalar-expr-4> ::= scalar-expr-5 | expr-addition | expr-subtraction
<scalar-expr-5> ::= scalar-expr-6 | expr-multiplication | expr-division

expr-disjunction ::= scalar-expr-0 ws #'(?i)OR'  ws scalar-expr-1
expr-conjunction ::= scalar-expr-1 ws #'(?i)AND' ws scalar-expr-2

expr-not ::= #'(?i)NOT' ws scalar-expr-3

expr-binop ::= scalar-expr-3 ws? binop ws? scalar-expr-4

expr-addition    ::= scalar-expr-4 ws? '+' ws? scalar-expr-5
expr-subtraction ::= scalar-expr-4 ws? '-' ws? scalar-expr-5

expr-multiplication ::= scalar-expr-5 ws? '*' ws? scalar-expr-6
expr-division       ::= scalar-expr-5 ws? '/' ws? scalar-expr-6

scalar-expr-group ::= '(' ws? scalar-expr ws? ')'

(* insert-expr *)

insert-expr ::= #'(?i)INSERT' ws #'(?i)INTO' ws relation-expr ws relation-expr

relation-value ::= '(' ws? simple-symbol-list ws? ')' ws #'(?i)VALUES' ws value-lists

simple-symbol-list ::= simple-symbol (ws? ',' ws simple-symbol)*
value-list ::= '(' ws? value (ws? ',' ws? value)* ws? ')'
value-lists ::= value-lists-full | value-lists-sparse
value-lists-full ::= value-list (ws? ',' ws? value-list)*
value-lists-sparse ::= '...' ws? nat ws? ':' ws? value-list (ws? ',' ws? nat ws? ':' ws? value-list)* ws? '...'

(* update-expr *)

update-expr ::= #'(?i)UPDATE' ws relation-expr
                ws #'(?i)SET' ws update-settings
                (ws #'(?i)WHERE' ws scalar-expr)?
update-settings ::= update-setting (ws? ',' ws? update-setting)*
update-setting ::= simple-symbol ws? '=' ws? scalar-expr

(* alter-expr *)

alter-expr ::= #'(?i)ALTER' ws relation-expr ws #'(?i)ADD' ws simple-symbol

(* rename-expr *)

rename-expr ::= relation-expr ws alias-clause

(* join-expr *)

join-expr ::= join-expr-0

<join-expr-0> ::= join-expr-1 | cross-join-expr | inner-join-expr
<join-expr-1> ::= join-expr-group

cross-join-expr ::= relation-expr ws #'(?i)CROSS' ws #'(?i)JOIN' ws relation-expr
inner-join-expr ::= relation-expr (ws #'(?i)INNER')? ws #'(?i)JOIN' ws relation-expr ws #'(?i)ON' ws scalar-expr
join-expr-group ::= '(' join-expr ')'

(* generate-expr *)

generate-expr ::= #'(?i)GENERATE' ws generate-list ws #'(?i)UNDER' ws model-expr

(* conditioning, constraining, and events *)

distribution-event ::= distribution-event-0

<distribution-event-0> ::= distribution-event-1 | distribution-event-or
<distribution-event-1> ::= distribution-event-2 | distribution-event-and
<distribution-event-2> ::= distribution-event-3 | distribution-event-binop
<distribution-event-3> ::= distribution-event-group

distribution-event-or  ::= distribution-event-0 ws #'(?i)OR'  ws distribution-event-1
distribution-event-and ::= distribution-event-1 ws #'(?i)AND' ws distribution-event-2

distribution-event-group ::= '(' ws? distribution-event ws? ')'

constrained-by-expr ::= model-expr
                        ws #'(?i)CONSTRAINED' ws #'(?i)BY'
                        ws distribution-event

density-event ::= density-event-0

<density-event-0> ::= density-event-1 | density-event-and
<density-event-1> ::= density-event-2 | density-event-eq
<density-event-2> ::= density-event-group

density-event-and ::= density-event-1 (ws #'(?i)AND' ws density-event-1)+

density-event-group ::= '(' ws? density-event ws? ')'

conditioned-by-expr ::= model-expr ws #'(?i)CONDITIONED' ws #'(?i)BY' ws density-event

incorporate-expr ::= #'(?i)INCORPORATE' ws relation-expr ws #'(?i)INTO' ws model-expr

variable ::= #'(?i)VAR' ws simple-symbol

variable-list ::= variable (ws? ',' ws? variable)*

(* binop *)

binop ::= '>' | '>=' | '=' | '<=' | '<' | is | is-not
is ::= #'(?i)IS'
is-not ::= #'(?i)IS' ws #'(?i)NOT'

(* literals *)

value         ::= float | int | bool | string | null
bool          ::= #'true|false'
float         ::= #'-?\d+\.\d+(E-?\d+)?'
int           ::= #'-?\d+'
nat           ::= #'\d+'
simple-symbol ::= #'(?!G__)[^0-9\s][\w\-\_\?\.]*'
string        ::= #'\"([^\"]*)\"'
null          ::= #'(?i)NULL'

(* whitespace *)

ws ::= #'\s+'
