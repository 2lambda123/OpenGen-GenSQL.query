<scalar-expr-0> ::= scalar-expr-1 | expr-disjunction | probability-expr | mutual-info-expr
<scalar-expr-7> ::= scalar-expr-group | identifier | value

(* model-expr *)

model-expr ::= '(' ws? model-expr ws? ')'
             | generative-table-expr
             | given-expr
             | conditioned-by-expr
             | constrained-by-expr
             | identifier

(* permissive event *)

<permissive-event-list> ::= (density-event-list / distribution-event-list)
                          | standalone-event-list
                          | standalone-event-conjunction
density-event-list ::= density-event (ws? ',' ws? density-event)*
distribution-event-list ::= distribution-event (ws? ',' ws? distribution-event)*

distribution-event-binop ::= identifier ws? binop ws? scalar-expr-3
                           / scalar-expr-3 ws? binop ws? identifier

density-event-eq ::= identifier ws? '=' ws? scalar-expr-3
                   / scalar-expr-3 ws? '=' ws? identifier

standalone-event-list ::= standalone-event (ws? ',' ws? standalone-event)*
<standalone-event> ::= standalone-event-conjunction / identifier
standalone-event-conjunction ::= identifier (ws #'(?i)AND' ws identifier)+

(* given *)

given-expr ::= model-expr ws #'(?i)GIVEN' ws (given-event-list | given-event-conjunction)
given-event-list ::= given-event (ws? ',' ws? given-event)*
given-event-conjunction ::= given-event (ws #'(?i)AND' ws given-event)+

<given-event> ::= (density-event-eq / distribution-event-binop)
                | identifier

(* overrides *)

probability-expr ::= #'(?i)PROBABILITY' ws #'(?i)OF'
                     ws permissive-event-list
                     ws #'(?i)UNDER' ws model-expr

(* generate-expr *)

<generate-list> ::= star / identifier-list

(* mutual-information-expr *)

mutual-info-expr ::= #'(?i)MUTUAL' ws #'(?i)INFORMATION'
                     ws #'(?i)OF' ws identifier-list
                     ws #'(?i)WITH' ws identifier-list
                     ws #'(?i)UNDER' ws model-expr
                   | #'(?i)MUTUAL' ws #'(?i)INFORMATION'
                     ws #'(?i)OF' ws distribution-event
                     ws #'(?i)WITH' ws distribution-event
                     ws #'(?i)UNDER' ws model-expr
