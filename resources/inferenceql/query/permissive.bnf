<scalar-expr-0> ::= scalar-expr-1 | expr-disjunction | probability-expr | mutual-info-expr
<scalar-expr-6> ::= scalar-expr-group | simple-symbol | value

(* model-expr *)

model-expr ::= '(' ws? model-expr ws? ')'
             | given-expr
             | conditioned-by-expr
             | constrained-by-expr
             | simple-symbol

(* permissive event *)

<permissive-event-list> ::= (density-event-list / distribution-event-list)
                          | standalone-event-list
                          | standalone-event-conjunction
density-event-list ::= density-event (ws? ',' ws? density-event)*
distribution-event-list ::= distribution-event (ws? ',' ws? distribution-event)*

distribution-event-binop ::= simple-symbol ws? binop ws? scalar-expr-3
                           / scalar-expr-3 ws? binop ws? simple-symbol

density-event-eq ::= simple-symbol ws? '=' ws? scalar-expr-3
                   / scalar-expr-3 ws? '=' ws? simple-symbol

standalone-event-list ::= standalone-event (ws? ',' ws? standalone-event)*
<standalone-event> ::= standalone-event-conjunction / simple-symbol
standalone-event-conjunction ::= simple-symbol (ws #'(?i)AND' ws simple-symbol)+

(* given *)

given-expr ::= model-expr ws #'(?i)GIVEN' ws (given-event-list | given-event-conjunction)
given-event-list ::= given-event (ws? ',' ws? given-event)*
given-event-conjunction ::= given-event (ws #'(?i)AND' ws given-event)+

<given-event> ::= (density-event-eq / distribution-event-binop)
                | simple-symbol

(* overrides *)

probability-expr ::= #'(?i)PROBABILITY' ws #'(?i)OF'
                     ws permissive-event-list
                     ws #'(?i)UNDER' ws model-expr

(* generate-expr *)

<generate-list> ::= star / simple-symbol-list

(* mutual-information-expr *)

mutual-info-expr ::= #'(?i)MUTUAL' ws #'(?i)INFORMATION'
                     ws #'(?i)OF' ws simple-symbol-list
                     ws #'(?i)WITH' ws simple-symbol-list
                     ws #'(?i)UNDER' ws model-expr
                   | #'(?i)MUTUAL' ws #'(?i)INFORMATION'
                     ws #'(?i)OF' ws distribution-event
                     ws #'(?i)WITH' ws distribution-event
                     ws #'(?i)UNDER' ws model-expr
